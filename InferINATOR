:- dynamic triple/3.

% Verifica se un'entità è una classe
is_class(Entity) :-
    triple(Entity, rdf:type, owl:'Class').

% Verifica se un'entità è una proprietà
is_property(Entity) :-
    triple(Entity, rdf:type, owl:'ObjectProperty').

% Assicura la bidirezionalità di una equivalenza tra classi
infer_equivalent_bidirectional(ClassA, ClassB) :-
    ( \+ triple(ClassB, owl:equivalentClass, ClassA) -> assert(triple(ClassB, owl:equivalentClass, ClassA)),
      format("Asserted bidirectional equivalence: ~w owl:equivalentClass ~w.\n", [ClassB, ClassA])
    ; true).

% Assicura la bidirezionalità di una disgiunzione tra classi
infer_disjoint_bidirectional(ClassA, ClassB) :-
    ( \+ triple(ClassB, owl:disjointWith, ClassA) -> assert(triple(ClassB, owl:disjointWith, ClassA)),
      format("Asserted bidirectional disjointness: ~w owl:disjointWith ~w.\n", [ClassB, ClassA])
    ; true).

% Chiamata per assicurare la bidirezionalità delle relazioni tra classi
infer_bidirectional_relations :-
    forall(triple(ClassA, owl:equivalentClass, ClassB), infer_equivalent_bidirectional(ClassA, ClassB)),
    forall(triple(ClassA, owl:disjointWith, ClassB), infer_disjoint_bidirectional(ClassA, ClassB)).

% Inferenza di relazioni basate sulle proprietà definite
infer_relations :-
    findall(_, (triple(S, P, O), is_property(P), infer_relation(S, P, O)), _).

%Viene ripetuta per tutte le triple grazie a infer_relations/0
infer_relation(S, P, O) :-
    triple(P, rdfs:domain, Domain),
    triple(P, rdfs:range, Range),
    (   \+ triple(S, rdf:type, Domain) -> assert(triple(S, rdf:type, Domain)); true),
    (   \+ triple(O, rdf:type, Range) -> assert(triple(O, rdf:type, Range)); true).

% Inferenza basata sulle sottoclassi
infer_subclass_relations :-
    findall(_, (triple(Entity, rdf:type, SubClass), infer_subclass_relation(Entity, SubClass)), _).

%Viene ripetuta per tutte le triple grazie a infer_subclass_relations/0
infer_subclass_relation(Entity, SubClass) :-
    triple(SubClass, rdfs:subClassOf, Class),
    (   \+ triple(Entity, rdf:type, Class) -> assert(triple(Entity, rdf:type, Class))).

% Inferenza basata sulle sotto proprietà
infer_subproperty_relations :-
    findall(_, (triple(S, SubP, O), infer_subproperty_relation(S, SubP, O)), _).

% Viene ripetuta per tutte le triple grazie a infer_subproperty_relations/0
infer_subproperty_relation(S, SubP, O) :-
    triple(SubP, rdfs:subPropertyOf, P),
    (   \+ triple(S, P, O) -> assert(triple(S, P, O))).

% Gestione dell'equivalenza tra classi
infer_equivalent_classes :-
    forall(triple(ClassA, owl:equivalentClass, ClassB),
                (forall(triple(Instance, rdf:type, ClassA),
                       ( \+ triple(Instance, rdf:type, ClassB) -> assert(triple(Instance, rdf:type, ClassB)) ; true)),
                forall(triple(Instance, rdf:type, ClassB),
                       ( \+ triple(Instance, rdf:type, ClassA) -> assert(triple(Instance, rdf:type, ClassA)) ; true)))).

% Aggiunge transitività alla disgiunzione tra classi
infer_transitive_disjointness :-
    forall((triple(ClassA, owl:equivalentClass, ClassB), triple(ClassA, owl:disjointWith, ClassC)),
           ( \+ triple(ClassB, owl:disjointWith, ClassC) -> assert(triple(ClassB, owl:disjointWith, ClassC)),
             format("Inferred and asserted disjointness: ~w owl:disjointWith ~w.\n", [ClassB, ClassC])
           ; true)),
    forall((triple(ClassB, owl:equivalentClass, ClassA), triple(ClassA, owl:disjointWith, ClassC)),
           ( \+ triple(ClassB, owl:disjointWith, ClassC) -> assert(triple(ClassB, owl:disjointWith, ClassC)),
             format("Inferred and asserted disjointness: ~w owl:disjointWith ~w.\n", [ClassB, ClassC])
           ; true)).

% Individua le inferenze che, per via della disgunzione tra
% classi, causano una inconsistenza
infer_disjoint_classes :-
    findall(Instance-(ClassA-ClassB),
            (triple(ClassA, owl:disjointWith, ClassB),
             triple(Instance, rdf:type, ClassA),
             triple(Instance, rdf:type, ClassB)),
            Inconsistencies),
    report_and_remove_inconsistencies(Inconsistencies).

% Gestisce la lista "Inconsistencies" rimuovendola dalle triple
% attualmente in considerazione e segnalando il problema
report_and_remove_inconsistencies([]).
report_and_remove_inconsistencies([Instance-(ClassA-ClassB) | Rest]) :-
    format('Inconsistency found: Instance ~w cannot belong to both disjoint classes ~w and ~w.\n', [Instance, ClassA, ClassB]),
    (triple(Instance, rdf:type, ClassA) -> retract(triple(Instance, rdf:type, ClassA)); true),
    (triple(Instance, rdf:type, ClassB) -> retract(triple(Instance, rdf:type, ClassB)); true),
    report_and_remove_inconsistencies(Rest).

%Gestione di uguaglianza tra individui
infer_same_as_relations:-
    forall(triple(IndividualA, owl:sameAs, IndividualB),
                (forall(triple(Instance, rdf:type, IndividualA),
                       ( \+ triple(Instance, rdf:type, IndividualB) -> assert(triple(Instance, rdf:type, IndividualB)) ; true)),
                forall(triple(Instance, rdf:type, IndividualB),
                       ( \+ triple(Instance, rdf:type, IndividualA) -> assert(triple(Instance, rdf:type, IndividualA)) ; true)))).



% Applicazione delle nuove inferenze
apply_inferences :-
    infer_bidirectional_relations,
    infer_relations,
    infer_subclass_relations,
    infer_subproperty_relations,
    (infer_transitive_disjointness,infer_bidirectional_relations),
    infer_disjoint_classes,
    infer_equivalent_classes,
    infer_same_as_relations.

% Stampa tutte le triple, sia quelle del dataset sia quelle inferite
print_all_triples :-
    forall(triple(S, P, O), format("~w ~w ~w.\n", [S, P, O])).

% Avvia l'esecuzione delle inferenze e stampa le triple risultanti
start_execution :-
    apply_inferences,
    print_all_triples.






% Definizioni di classi
triple(bird, rdf:type, owl:'Class').
triple(canary, rdf:type, owl:'Class').
triple(cat, rdf:type, owl:'Class').

% Equivalenza di classi
triple(canary, owl:equivalentClass, bird).

% Disgiunzione di classi
triple(bird, owl:disjointWith, cat).

% Istanze
triple(tweety, rdf:type, canary).
triple(sylvester, rdf:type, cat).
% triple(tweety, rdf:type, bird). Questa triple è coerente con
% l'equivalenza ma non è presente perche sono scemo e l'avrei dovuta
% mettere
triple(felix, rdf:type, bird).
triple(felix, rdf:type, canary).
triple(felix, rdf:type, cat). % Questa triple crea un'inconsistenza con la disgiunzione
